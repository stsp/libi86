dnl Copyright (c) 2018 TK Chia
dnl
dnl This file is free software; you can redistribute it and/or modify it
dnl under the terms of the GNU Lesser General Public License as published by
dnl the Free Software Foundation; either version 3 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful, but
dnl WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl Lesser General Public License for more details.
dnl
dnl You should have received a copy of the GNU Lesser General Public License
dnl along with this program; see the file COPYING3.LIB.  If not see
dnl <http://www.gnu.org/licenses/>.

AT_BANNER([[<dos.h> tests]])

AT_SETUP([[bdos, bdosptr]])
AT_LIBI86_CHECK(dnl
[[#include <dos.h>
int main (void)
{
#ifdef __MSDOS__
  bdosptr (9, "Hello $(ignore this)", 0);
  bdos (2, '$', 0);
  bdos (2, 'f', 0);
  bdos (2, 'r', 0);
  bdos (2, 'o', 0);
  bdos (2, 'm', 0);
  bdos (2, '$', 0);
  bdosptr (9, " bdos and bdosptr!\r\n$(and ignore this too)", 0);
#else
# error
#endif
  return 0;
}]],[[Hello $from$ bdos and bdosptr!
]])
AT_CLEANUP

AT_SETUP([[intdos]])
dnl This also checks that we can use <dos.h> without also including <i86.h>.
dnl To do this, we need to separately define FP_OFF (.) here rather than rely
dnl on <i86.h>'s definition...
AT_LIBI86_CHECK(dnl
[[#include <dos.h>
#include <stdlib.h>
#define FP_OFF(p) __builtin_ia16_FP_OFF (p)
int main (void)
{
  union REGS regs;
  int rv;
#ifdef __MSDOS__
  static char msg[] = "Hello from intdos!\r\n(ignore this part)";
  regs.h.ah = 0x40;
  regs.w.bx = 1;
  regs.w.cx = 20;
  regs.w.dx = FP_OFF (msg);
  rv = intdos (&regs, &regs);
  if (rv != 20)
    abort ();
  return 0;
#else
# error
#endif
}]],[[Hello from intdos!
]])
AT_CLEANUP
