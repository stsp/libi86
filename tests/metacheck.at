dnl Copyright (c) 2018--2020 TK Chia
dnl
dnl This file is free software; you can redistribute it and/or modify it
dnl under the terms of the GNU Lesser General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful, but
dnl WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl Lesser General Public License for more details.
dnl
dnl You should have received a copy of the GNU Lesser General Public License
dnl along with this program; see the file COPYING2.LIB.  If not see
dnl <http://www.gnu.org/licenses/>.

AT_BANNER([[Basic sanity (meta-)checks on the test setup]])

AT_SETUP([[dos2unix]])
AT_CHECK([[dos2unix --version]],,[[ignore]],[[ignore]])
AT_CLEANUP

AT_SETUP([[unix2dos]])
AT_CHECK([[unix2dos --version]],,[[ignore]],[[ignore]])
AT_CLEANUP

AT_SETUP([[dosemu]])

AT_CHECK([["$DOSEMU" $DOSEMU_OPTS --version]],,[[ignore]],[[ignore]])
AT_CHECK([AT_LIBI86_RUN(hello.com,"$abs_srcdir")]dnl
,,[[Hello world!
]],[[ignore]])

AT_CLEANUP

AT_SETUP([[GCC toolchain]])

AT_CHECK([[$CC --version -v]],,[[ignore]],[[ignore]])
AT_LIBI86_CHECK(dnl
[[#if ! defined __ia16__
# error
#endif
#include <stdio.h>
char dawg[] = "dawg";
int
main (void)
{
  printf ("yo %s\n", dawg);
  return 0;
}]],[[yo dawg
]])

AT_CLEANUP

AT_SETUP([[G++ toolchain]])

AT_SKIP_IF([test false = "$CXX"])
AT_LIBI86_CHECK_CXX(dnl
[[#if ! defined __ia16__ || ! defined __cplusplus
# error
#endif
/* We may build libi86 right after building the stage 1 GCC --- without
   building libstdc++ --- so do not assume that the Standard C Library's
   <cstdio> will be present at this point in time.  Just use the good old
   <stdio.h> instead.  */
#include <stdio.h>
char yo[] = "yo";
int
main (void)
{
  printf ("%s dawg\n", yo);
  return 0;
}]],[[yo dawg
]])

AT_CLEANUP

AT_SETUP([[Console output testing]])

AT_LIBI86_CHECK_CONSOLE(dnl
[[#include <stdio.h>
int main (void)
{
  printf("w00t\n\nw0000t\n");
  return 0;
}]],[[w00t

w0000t
]])

AT_CLEANUP
